defmodule AshPhoenixStarterWeb.AuthController do
  use AshPhoenixStarterWeb, :controller
  use AshAuthentication.Phoenix.Controller
  alias AshPhoenixStarter.Accounts.UserImpersonation

  def success(conn, activity, user, _token) do
    return_to = get_session(conn, :return_to) || ~p"/dashboard"

    message =
      case activity do
        {:confirm_new_user, :confirm} -> "Your email address has now been confirmed"
        {:password, :reset} -> "Your password has successfully been reset"
        _ -> "You are now signed in"
      end

    conn
    |> delete_session(:return_to)
    |> store_in_session(user)
    # If your resource has a different name, update the assign name here (i.e :current_admin)
    |> assign(:current_user, user)
    |> put_flash(:info, message)
    |> redirect(to: return_to)
  end

  def failure(conn, activity, reason) do
    message =
      case {activity, reason} do
        {_,
         %AshAuthentication.Errors.AuthenticationFailed{
           caused_by: %Ash.Error.Forbidden{
             errors: [%AshAuthentication.Errors.CannotConfirmUnconfirmedUser{}]
           }
         }} ->
          """
          You have already signed in another way, but have not confirmed your account.
          You can confirm your account using the link we sent to you, or by resetting your password.
          """

        _ ->
          "Incorrect email or password"
      end

    conn
    |> put_flash(:error, message)
    |> redirect(to: ~p"/sign-in")
  end

  def sign_out(conn, _params) do
    return_to = get_session(conn, :return_to) || ~p"/"

    conn
    |> clear_session(:AshPhoenixStarter)
    |> put_flash(:info, "You are now signed out")
    |> redirect(to: return_to)
  end

  def impersonate(conn, %{"user_id" => user_id}) do
    # 1. Prevent unauthorized cross tenant access
    current_user = conn.assigns.current_user

    if AshPhoenixStarterWeb.Helpers.is_super_user?(current_user) do
      # Store original session token for reversion
      # Ash stores under "user_token" by default (confirm if customized)
      conn
      # Sign in as the target user
      |> force_sign_in(user_id)
      |> put_session(:impersonator_user_id, current_user.id)
      |> mark_impersonation_started()
      |> put_flash(:info, "Now impersonating")
      |> redirect(to: ~p"/dashboard")
    else
      conn
      |> put_flash(:error, "You are not allowed to impersonate other users")
      |> redirect(to: ~p"/dashboard")
    end
  end

  def stop_impersonating(conn, _params) do
    current_user_id = get_session(conn, :impersonator_user_id, false)

    if current_user_id do
      # Restore original user session
      conn
      |> force_sign_in(current_user_id)
      |> delete_session(:impersonator_user_id)
      |> mark_impersonation_stopped()
      |> put_flash(:info, "Impersonation ended. Back to your account.")
      |> redirect(to: ~p"/dashboard")
    else
      conn
      |> put_flash(:error, "No impersonation active.")
      |> redirect(to: ~p"/dashboard")
    end
  end

  defp force_sign_in(conn, current_user_id, perpose \\ "Impersonation") do
    %{conn: conn, purpose: perpose, user_id: current_user_id}
    |> AshPhoenixStarter.Accounts.User.force_sign_in!(authorize?: false)
  end

  defp mark_impersonation_started(conn) do
    params = %{
      reason: "Impersonating user for support",
      impersonator_user_id: get_session(conn, :impersonator_user_id),
      impersonated_user_id: conn.assigns.current_user.id
    }

    {:ok, _user_imp} = UserImpersonation.start(params, authorize?: false)

    conn
  end

  defp mark_impersonation_stopped(conn) do
    params = %{impersonator_user_id: conn.assigns.current_user.id}

    {:ok, _user_imp} = UserImpersonation.stop(params, authorize?: false)

    conn
  end
end
